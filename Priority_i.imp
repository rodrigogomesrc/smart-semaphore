/* Priority_i
 * Author: rodri
 * Creation date: 11/07/2025
 */

IMPLEMENTATION Priority_i

REFINES Priority
      
SEES
    SmartSemaphore_ctx
    
VALUES

    SIZE = 4;
    BOUND = 3

CONCRETE_VARIABLES
    cursor,
    order_i

INVARIANT
    cursor: 0..BOUND &
    order_i: 0..3 --> STATE &
    order = %kk . (kk : 1..SIZE | order_i((cursor + kk - 1) mod SIZE)) &
    dom(order_i) = 0..BOUND

INITIALISATION
    BEGIN
        cursor := 0;
        order_i := {0 |-> NS, 1 |-> LO, 2 |-> DNS, 3 |-> DLO}
       
    END

OPERATIONS

    ss <-- current =
    BEGIN
        ss := order_i(cursor)
    END;

    ss <-- next =
    BEGIN
        ss := order_i((cursor + 1) mod SIZE)
    END;

    ss <-- advance =
    BEGIN
        ss := order_i(cursor);
        cursor := (cursor + 1) mod SIZE
    END
END